plugins {
    id 'net.minecrell.licenser' version '0.4.1' apply false
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
}

allprojects {
    apply plugin: 'net.minecrell.licenser'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = 'net.thiccaxe.resonance'
    version = '1.2.0-SNAPSHOT'


    sourceCompatibility = 11
    targetCompatibility = 11

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jar {
        from '../LICENSE'
    }

    project.ext.majorVersion = '1'
    project.ext.minorVersion = '2'
    project.ext.patchVersion = '0'
    project.ext.apiVersion = project.ext.majorVersion + '.' + project.ext.minorVersion
    project.ext.fullVersion = project.ext.apiVersion + '.' + project.ext.patchVersion

    license {
        header = rootProject.file('HEADER.txt')
        include '**/*.java'
        newLine = true
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/thiccaxe/Resonance")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }


    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}


subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'

    defaultTasks 'licenseFormat', 'build'

    dependencies {
        // Jetbrains Annotations
        implementation 'org.jetbrains:annotations:20.1.0'

        // Netty
        compileOnly group: 'io.netty', name: 'netty-all', version: '4.1.63.Final'

        // Google Guava
        compileOnly group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    }
}