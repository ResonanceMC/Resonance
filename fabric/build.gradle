plugins {
    id 'com.github.johnrengelman.shadow'
    id 'fabric-loom' version '0.8-SNAPSHOT'
}

configurations {
    implementation.extendsFrom shade
}

repositories {
    mavenLocal()
}

dependencies {
    shade(project(':resonance-common'))

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    def targetJava = 16
    targetCompatibility = JavaVersion.toVersion(targetJava)
    sourceCompatibility = JavaVersion.toVersion(targetJava)
}

tasks {
    jar {
        archiveClassifier.set("dev")
    }
    shadowJar {
        archiveClassifier.set("shadow")
        configurations = [project.configurations.shade]

        relocate 'org.spongepowered.configurate', 'net.thiccaxe.resonance.lib.configurate'
    }
    remapJar {
        archiveClassifier.set(null)
        input.set(shadowJar.archiveFile)
        destinationDirectory = file '../build/release'
        archiveFileName.set("Resonance-${project.version}-fabric-${project.minecraft_version}.jar")
    }
    build {
        dependsOn(remapJar)
    }
}