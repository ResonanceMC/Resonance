import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'com.github.johnrengelman.shadow'
    id 'fabric-loom' version '0.9.45'
}

configurations {
    implementation.extendsFrom shade
}

repositories {
    mavenLocal()
}

dependencies {
    shade(project(':resonance-common'))

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

task relocateShadowJar (type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "net.thiccaxe.resonance.libs"
}

tasks {

    jar {
        archiveClassifier.set("dev")
    }
    shadowJar {
        archiveClassifier.set("shadow")
        configurations = [project.configurations.shade]

        dependsOn(tasks.relocateShadowJar)
    }
    remapJar {
        archiveClassifier.set(null)
        input.set(shadowJar.archiveFile)
        destinationDirectory = file '../build/release'
        archiveFileName.set("Resonance-${project.version}-fabric-${project.minecraft_version}.jar")
    }
    build {
        dependsOn(remapJar)
    }
}
