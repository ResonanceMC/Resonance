plugins {
    id('com.github.johnrengelman.shadow') version '7.0.0'
    id('fabric-loom') version '0.9.4'
}

configurations {
    implementation.extendsFrom shade
}

dependencies {
    shade(project(':resonance-common'))

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.shadowJar {
    archiveClassifier.set("shaded")
    relocate('org.spongepowered', 'net.thiccaxe.libs.org.spongepowered')
    relocate('io.papermc', 'net.thiccaxe.libs.io.papermc')
    relocate('org.spongepowered', 'net.thiccaxe.libs.org.spongepowered')
    relocate('io.leangen.geantyref', 'net.thiccaxe.libs.io.leangen.geantyref')
    relocate('com.typesafe.config', 'net.thiccaxe.libs.com.typesafe.config')
    relocate('org.slf4j', 'net.thiccaxe.libs.org.slf4j')
    configurations = [project.configurations.shade]
}
tasks.remapJar {

    archiveClassifier.set(null)
    input.set(shadowJar.archiveFile)
    destinationDirectory = file '../build/release'
    archiveFileName.set("Resonance-${project.version}-fabric-${project.minecraft_version}.jar")
}
build {
    dependsOn(remapJar)
}

